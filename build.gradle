plugins {
    id 'io.spring.dependency-management' version '1.0.3.RELEASE' apply false
    id 'net.ltgt.apt' version '0.9' apply false
    id 'org.unbroken-dome.test-sets' version '1.4.2' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.1.51' apply false
    id 'nebula.optional-base' version '3.2.0' apply false
    id 'com.jfrog.bintray' version '1.7' apply false
}

apply from: 'gradle/versioning.gradle'


configure(subprojects) { project ->

    project.plugins.withId('java') {
        project.sourceCompatibility = 1.8
        project.targetCompatibility = 1.8

        project.apply plugin: 'io.spring.dependency-management'

        project.dependencies {
            compileOnly 'com.google.code.findbugs:jsr305'
            testCompile 'junit:junit'
            testCompile 'com.google.truth:truth'
            testCompile 'org.slf4j:slf4j-api'
            testRuntime 'ch.qos.logback:logback-classic'
        }
    }

    project.plugins.withId('groovy') {
        project.dependencies {
            testCompile 'org.spockframework:spock-core'
        }
    }
    
    project.plugins.withId('io.spring.dependency-management') {
        project.apply from: "$rootDir/gradle/dependencies.gradle"
    }

    project.apply from: "$rootDir/gradle/publishing.gradle"
}
